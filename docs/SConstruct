
import os

Progress('$TARGET\r',overwrite=True)

config = Variables('.sconsrc', ARGUMENTS)
targets = ['html','latexpdf','info','dirhtml','singlehtml','htmlhelp','qthelp','devhelp','epub','applehelp','latex','man','texinfo','text','gettext','doctest','linkcheck','xml','pseudoxml']
config.AddVariables(
    EnumVariable('default','default target','html',targets),
    PathVariable('sourcedir','sphinx configuration directory','.',PathVariable.PathIsDir),
    PathVariable('builddir','build directory','_build',PathVariable.PathIsDirCreate),
    PathVariable('config','sphinx config file','conf.py'),
    ('builder','sphinx build','sphinx-build')
)

env = Environment(ENV=os.environ,tools=[],variables=config)
config.Update(env)
config.Save('.sconsrc', env)

#set up targets
for name in targets:
    target = Dir(name, env['builddir'])
    env.Command(name, env['config'], f'$builder -M $TARGET $sourcedir $builddir', chdir=True)
    #env.Depends(targets, source)
    env.AlwaysBuild(name)
    env.Alias(target,name)
    env.Clean(name, target)
    env.Clean('all',target)
Clean('all',os.path.join(env['builddir'],'doctrees'))
Default(env['default'])

Help('\nConfiguration variables:')
for line in config.GenerateHelpText(env).split('\n'):
    Help('\n    ' + line)